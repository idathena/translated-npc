//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Kita akan berangkat menuju Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Kita akan tiba di Einbroch sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Einbroch. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Sekarang kita ada di Einbroch. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Kita akan berangkat menuju Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Kita akan tiba di Lighthalzen sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Lighthalzen. Selamat bersenang-senang!",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Sekarang kita ada di Lighthalzen. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Kita akan berangkat menuju Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Kita akan tiba di Einbroch sebentar lagi.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Einbroch. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Sekarang kita ada di Einbroch. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Kita akan berangkat menuju Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Kita akan tiba di Juno sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Sekarang kita ada di Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Kita akan berangkat menuju Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Kita akan tiba di Hugel sebentar lagi.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Hugel. Selamat bersenang-senang!",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Sekarang kita ada di Hugel. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Kita akan berangkat menuju Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Kita akan tiba di Juno sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Sekarang kita ada di Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Kru Pesawat#ein-1	852,{
	mes "[Kru Pesawat]";
	mes "Setelah pesawatnya sudah tiba di tempat tujuan, kamu bisa menggunakan tangga ke atas";
	mes "untuk meninggalkan pesawat ini.";
	mes "Terima kasih!";
	close;
}

airplane,64,94,1	script	Umbala Anak Kecil#ein_p	787,{
	emotion e_swt2;
	mes "[Anak Kecil]";
	if (event_umbala >= 3) {
		mes "Wow, ibu!";
		mes "Lihatlah!";
		mes "Kita sedang terbang! K-Kita sedang...";
		mes "Kita ada di atas awan!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Umbala Perempuan#ein_p	783,{
	emotion e_dots;
	mes "[Perempuan]";
	if (event_umbala >= 3) {
		mes "Sssttt...";
		mes "Anakku, diamlah~";
		mes "Jangan berteriak-teriak seperti itu saat kita ada di tempat umum!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Halo~";
		mes "Kelihatannya kamu bisa mengerti bahasaku.";
		mes "^333333*Whew...!*^000000";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Kalau kamu tidak tahu, aku sudah membantu perusahaan pesawat terbang ini.";
		mes "Aku melatih para kru pesawat ini di Umbala untuk mengatasi perasaan takut mereka";
		mes "dengan cara loncat bungee. Menarik kan?";
		next;
		mes "[Chrmlim]";
		mes "Tapi...";
		mes "Beberapa dari mereka tidak bisa mengatasi rasa takutnya.";
		mes "Ada beberapa, um, yang berakhir di ^333333in Nifflheim^000000 malah...";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion e_pif;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Staf Pesawat#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Staf Pesawat]";
		mes "Selamat datang di pesawat ini!";
		mes "Ada yang bisa kubantu?";
		next;
		select("Apakah ada penumpang yang bernama Thierry?");
		mes "[Staf Pesawat]";
		mes "Maaf, kurasa tidak ada penumpang yang bernama Thierry.";
		close;
	}
	// Hugel quest end
	mes "[Staf Pesawat]";
	mes "Selamat datang di pesawat ini!";
	mes "Ada yang bisa kubantu?";
	next;
	switch (select("Aku ingin naik pesawat:Kabin Kapten:Fasilitas:Batal")) {
	case 1:
		mes "[Staf Pesawat]";
		mes "Kamu akan mendengar pemberitahuan bila pesawatnya sudah tiba di tujuan.";
		mes "Silakan gunakan salah satu pintu keluar, ada di sebelah Utara dan Selatan dari pesawat ini.";
		next;
		mes "[Staf Pesawat]";
		mes "Kalau tujuanmu sudah terlewat, jangan khawatir.";
		mes "Pesawat ini selalu mengudara di rute yang sudah ditentukan.";
		mes "Jadi pesawat ini pasti akan tiba di tempat tujuanmu lagi.";
		close;
	case 2:
		mes "[Staf Pesawat]";
		mes "Kabin Kapten ada di depan pesawat ini.";
		mes "Di sana kamu bisa bertemu dengan sang kapten dan sang pilot.";
		close;
	case 3:
		mes "[Staf Pesawat]";
		mes "Pesawat ini menyediakan beberapa Mini Games untuk menghibur para penumpang.";
		mes "Silakan coba keberuntungan dan keahlianmu di dalam Mini Games tersebut~";
		close;
	case 4:
		mes "[Staf Pesawat]";
		mes "Terima kasih, dan selamat bersenang-senang!";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Oh, halo!";
	mes "Sekarang aku sedang menjalani perjalanan suci,";
	mes "aku berdoa untuk kemajuan dan kesejahteraan Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Um, pesawat ini sedang menuju ke Lutie, iya kan?";
	mes "Aku sudah menunggu lama, tapi aku belum mendengar pemberitahuan tentang Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "Langitnya terlihat begitu berbeda dan sangat indah dari atas sini!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "Kuharap aku bisa meminum bir dingin yang segar.";
		mes "Minum bir adalah tujuan hidupku! Minum bir memberiku energi!";
		mes "Aku tidak bisa hidup tanpa bir!";
		next;
		mes "[Pilot]";
		mes "Tapi! Menjalankan pesawat saat mabuk, sangatlah tidak baik.";
		mes "Tapi! Itu membuatku ingin minum bir lagi dan lagi!";
		emotion e_sob;
		next;
		select("Apakah ada penumpang yang bernama Thierry?");
		mes "[Pilot]";
		mes "Seragam ini sangat cocok dengan warna pesawat ini.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "Belum ada yang pernah datang ke ruangan ini sebelumnya.";
		mes "Si kapten adalah seekor rusa. Aku ingin mengenakan celana dalamku...";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Wah!? Siapa kamu!";
		next;
		mes "- ...Kelihatannya dia tidak mendengarkanmu sama sekali -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Sudah lama sekali sejak aku bisa menikmati bir dingin yang nikmat.";
		mes "Tapi pekerjaanku menuntutku agar aku selalu sadar setiap waktu!";
		next;
		mes "[Pilot]";
		mes "Jangan pernah mabuk sembarangan!";
		mes "Tapi aku masih ingat, saat terakhir kali aku pergi berlibur.";
		mes "Yeap, sedikit bir, keripik kentang, TV. Rasanya hidup indah sekali!";
		emotion e_sob;
		close;
	case 2:
		mes "[Pilot]";
		mes "Oooh cuacanya sangat bagus hari ini.";
		mes "Sangat cerah, cocok untuk menjalankan pesawat dengan santai.";
		close;
	case 3:
		mes "[Pilot]";
		mes "kamu tahu, kapten kami adalah seorang yang dihormati.";
		mes "Dia dan adiknya adalah orang yang sudah dikenal di bisnis ini.";
		next;
		mes "[Pilot]";
		mes "Hanya antara kita berdua, aku akan memberitahumu sesuatu.";
		mes "Santa itu sepertinya menyembunyikan sesuatu.";
		mes "Dia memperkerjakan rusa dan para elf.";
		mes "Dia sangat jenius!";
		close;
	default:
		mes "[Pilot]";
		mes "Kamu tahu, cara mengemudikan pesawat ini masih terasa aneh.";
		mes "Mungkin karena mereka membuat pesawat ini dengan terburu-buru.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Tetapi mereka benar-benar serius, mereka sudah berpikir ke depan.";
		mes "Maksudku, mereka sudah melatih para kru saat pesawatnya masih dalam tahap pengembangan.";
		mes "Gila kan?!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "Selamat datang di toko Fruitz's.";
	mes "Kamu bisa membeli Apel atau memprosesnya menjadi Jus Apel.";
	next;
	switch (select("Beli Apel.:Buat Jus Apel.:Kenapa kamu ada di sini?:Batal.")) {
	case 1:
		mes "[Fruitz]";
		mes "Silakan tulis jumlah Apel yang ingin kamu beli.";
		mes "Harganya 15 zeny per buah, dan kamu bisa membeli sampai 500 buah.";
		mes "Ketik '0' untuk membatalkan.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Terima kasih telah datang ke tokoku.";
				mes "Kapan-kapan datang lagi ya~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Kamu hanya bisa membeli 500 buah per transaksi.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Harga ^FF0000" + .@input + "^000000 buah Apel adalah ";
				mes "^FF0000" + .@pay + " ^000000 zeny.";
				mes "Apakah kamu ingin membelinya?";
				next;
				if (select("Ya:Tidak") == 2) {
					mes "[Fruitz]";
					mes "Terima kasih telah datang ke tokoku.";
					mes "Kapan-kapan datang lagi ya~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Maaf, tapi kamu tidak mempunyai uang yang cukup.";
			mes "Silakan periksa dulu uangmu, atau belilah dalam jumlah yang lebih kecil.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, sepertinya kamu tidak mempunyai ruang yang cukup di dalam tasmu.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kuharap kamu suka Apelnya~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Oke, aku butuh ";
		mes "^FF00003 Apel dan 1 Empty Bottle^000000";	//nanakiwurtz: Empty Bottle = Botol Kosong?
		mes "untuk membuat 1 Jus Apel.";
		mes "Apakah kamu ingin membuat Jus Apel?";
		next;
		switch (select("Ya:Tidak")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Maaf, kamu tidak mempunyai bahan yang cukup untuk membuat Jus Apel.";
				mes "Ingat, aku butuh 3 Apel dan 1 Empty Bottle.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Terima kasih, silakan tunggu sebentar.";
				next;
				mes "^3355FF*Peras peras*";
				mes "*Peras peras*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Fruitz]";
				mes "Silakaaaan~";
				mes "Selamat menikmati!";
				mes "Silakan datang lagi kalau kamu ingin Jus Apel lagi ya!";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Terima kasih telah datang ke tokoku.";
			mes "Kapan-kapan datang lagi ya~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Dulu aku adalah seorang pengelana.";
		mes "Suatu hari, saat aku sedang tidur siang, sesuatu mengenai kepalaku dan membangunkanku.";
		next;
		mes "[Fruitz]";
		mes "Ternyata aku sedang tidur di bawah pohon apel, dan sebuah apel jatuh di atas kepalaku.";
		mes "Saat itu aku sedang kelaparan dan hampir saja memakan apel tadi...";
		next;
		mes "[Fruitz]";
		mes "Tapi tiba-tiba Kain, teman lamaku dari pertambangan memintaku untuk membantunya di dalam pesawat.";
		mes "Jadi aku membantunya di dalam pesawat dan aku melihat beberapa orang yang sedang bermain dadu.";
		next;
		mes "[Fruitz]";
		mes "Aku sedang bosan dan penasaran, dan akhirnya aku malah ikut main.";
		mes "Aku tidak tahu mengapa aku bisa begitu beruntung!";
		mes "Hadiahnya bertambah besar, dari satu apel jadi dua...";
		next;
		mes "[Fruitz]";
		mes "Dua jadi empat, dan sebelum aku menyadarinya, aku sudah mengumpulkan banyak sekali apel!";
		mes "Jadi aku mulai menjalankan bisnisku sendiri di dalam pesawat ini dengan menjual apel.";
		mes "Cukup aneh ya?";
		next;
		mes "[Fruitz]";
		mes "Jadi, apel sangat bagus untuk kesehatanmu.";
		mes "Aku juga sangat menyukainya. Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Terima kasih telah datang ke tokoku.";
		mes "Kapan-kapan datang lagi ya~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Kita akan berangkat menuju Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Kita akan tiba di Izlude sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation2,1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Izlude. Selamat bersenang-senang!",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","We are currently in Izlude. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Kita akan berangkat menuju Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Kita akan tiba di Juno sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation2,2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Juno. Selamat bersenang-senang!",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","We are currently in Juno. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Kita akan berangkat menuju Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Kita akan tiba di Rachel sebentar lagi.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	set $@airplanelocation2,0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Selamat datang di Rachel. Selamat bersenang-senang!",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","We are currently in Rachel. Pesawatnya akan berangkat sebentar lagi.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","Pesawatnya sudah berangkat. Tujuan kita berikutnya adalah Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	set .moninv, .moninv + 1;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		set .moninv, 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Staf Pesawat#airplane01	67,{
	mes "[Staf Pesawat]";
	mes "Welcome";
	mes "to the Airship.";
	mes "How may I help you?";
	next;
	switch (select("Using the Airship:Captain's Cabin:Facilities:Cancel")) {
	case 1:
		mes "[Staf Pesawat]";
		mes "When you see a broadcast";
		mes "announcing that we have";
		mes "arrived at your destination,";
		mes "please use one of the exits";
		mes "located at the north and";
		mes "south ends of the Airship.";
		next;
		mes "[Staf Pesawat]";
		mes "If you happen to miss";
		mes "your stop, don't worry.";
		mes "The Airship is constantly";
		mes "en route and you'll get";
		mes "another chance to arrive";
		mes "to your intended destination.";
		close;
	case 2:
		mes "[Staf Pesawat]";
		mes "The Captain's Cabin";
		mes "is located at the front";
		mes "of the Airship. There, you";
		mes "can meet the captain and";
		mes "the pilot of the Airship.";
		close;
	case 3:
		mes "[Staf Pesawat]";
		mes "The Airship provides";
		mes "various Mini Games for";
		mes "the entertainment of all";
		mes "our passengers. We invite";
		mes "you to try your luck and skills";
		mes "in the Airship's Mini Games~";
		close;
	case 4:
		mes "[Staf Pesawat]";
		mes "Well, I hope you";
		mes "your flight aboard";
		mes "our Airships. Thank";
		mes "you and have a good day.";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you loose some weight. -";
		close;
	}
	mes "[Meltz]";
	mes "Selamat datang di Meltz's";
	mes "Shop where you can";
	mes "purchase Apples or grind";
	mes "them to make Apple Juice.";
	next;
	switch (select("Buy Apples.:Make Apple Juice.:Cancel.")) {
	case 1:
		mes "[Meltz]";
		mes "Please enter the amount";
		mes "of Apples that you wish to";
		mes "buy. Each Apple is 15 zeny";
		mes "and you can buy a maximum";
		mes "of 500 at a time. Please enter";
		mes "'0' to cancel your order.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Thanks for stopping";
				mes "by my shop. Farewell!";
				mes "Come by anytime when";
				mes "you feel like having an";
				mes "Apple to snack on~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "You've entered a number";
				mes "higher than the maximum";
				mes "value of 500. Please enter";
				mes "the number of Apples you";
				mes "wish to purchase again.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "A total of ^FF0000" + .@input + "^000000 Apples";
				mes "will cost you ^FF0000" + .@pay + "^000000 zeny.";
				mes "Would you like to continue?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Thanks for stopping";
					mes "by my shop. Farewell!";
					mes "Come by anytime when";
					mes "you feel like having an";
					mes "Apple to snack on~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "I'm sorry, you don't have";
			mes "enough money with you.";
			mes "Please check your funds or";
			mes "purchase less Apples.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, I don't think you've";
			mes "got enough room to carry";
			mes "this many Apples. You might";
			mes "want to free up your inventory";
			mes "space.";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Thanks for stopping by";
			mes "my shop. I hope you enjoy";
			mes "the flavor of these Apples~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Okay, I'll need";
		mes "^FF00003 Apples and 1 Empty Bottle^000000";
		mes "to make 1 Apple Juice for you.";
		mes "Would you like to proceed?";
		next;
		switch (select("Yes:No")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "I'm sorry, but you don't";
				mes "have enough materials to";
				mes "create a bottle of Apple Juice.";
				mes "Remember, I need 3 Apples";
				mes "and 1 Empty Bottle to do it.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Apple
				delitem 713,1; //Empty_Bottle
				getitem 531,1; //Apple_Juice
				mes "[Meltz]";
				mes "There you go~";
				mes "Please come again.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Thanks for stopping";
			mes "by my shop. Farewell!";
			mes "Come by anytime when";
			mes "you feel like having an";
			mes "Apple to snack on~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Thanks for stopping";
		mes "by my shop. Farewell!";
		mes "Come by anytime when";
		mes "you feel like having an";
		mes "Apple to snack on~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Longitude, 131 degrees east.";
		mes "Latitude, 37 degrees north.";
		mes "We're right on course, captain.";
		close;
	case 2:
		mes "[Pilot]";
		mes "Looks like a really";
		mes "cloudy day. Always hard";
		mes "to navigate when the skies";
		mes "aren't clear. Guess we'll";
		mes "need to amp the radar.";
		close;
	case 3:
		mes "[Pilot]";
		mes "The Captain is a good";
		mes "man and I can't think of";
		mes "a finer person to command";
		mes "this ship. Still, he's pretty";
		mes "tough, a real slave driver.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Hey...!^000000";
		mes "^ff0000Less chit-chat^000000";
		mes "^ff0000and more piloting!^000000";
		next;
		mes "[Pilot]";
		mes "R-right away, sir!";
		mes "(See what I mean?)";
		close;
	default:
		mes "[Pilot]";
		mes "This uniform is";
		mes "really dapper, but";
		mes "it's way too thick to";
		mes "wear around the Airship.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "No one ever really";
		mes "comes into this room.";
		mes "And the captain IS a reindeer.";
		mes "I could just strip to my boxers.";
		next;
		emotion e_omg;
		mes "[Pilot]";
		mes "Oh...! Hello there!";
		mes "E-e-enjoying your flight?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "It's so weird!";
	mes "I went to visit the";
	mes "Airship Captain and";
	mes "all I saw was this";
	mes "weird reindeer. Oh!";
	mes "Do you think that...";
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "As I expected, the";
	mes "in-flight meals are";
	mes "three star quality at best.";
	mes "*Harrrumph* I really should";
	mes "have brought my chef so that";
	mes "I could enjoy a real meal.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Swordsman Shimizu]";
	mes "Finally, after five";
	mes "years of waiting...";
	mes "I can have my revenge!";
	next;
	mes "[Swordsman Shimizu]";
	mes "I just...";
	mes "Have to make sure that";
	mes "I don't keep missing my";
	mes "stop. But soon, very soon,";
	mes "vengeance will be mine!";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Selamat datang di the";
	mes "^ff0000RO Typing Challenge^000000.";
	mes "Would you like to play";
	mes "a quick typing game?";
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000:Information:View Top Records:Cancel")) {
	case 1:
		mes "[Nils]";
		mes "Okay, we have";
		mes "a new challenger!";
		mes "Enter the following";
		mes "text as quickly as you";
		mes "can without making any";
		mes "mistakes! Let's start~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save1$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		next;
		input .@save2$;
		set .@end_time, gettime(3)*60*60 + gettime(2)*60 + gettime(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Your record is ^ff0000" + .@total_time + " seconds^000000 and";
			mes "the total letters are " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Hmmm, this record isn't";
				mes "humanly possible unless you";
				mes "copy and paste the whole";
				mes "sentence. Please play fairly";
				mes "next time.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "The previous top record was";
				mes "made by ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "with the total ^0000ff" + $050320_ein_typing + "^000000 letters.";
				mes "However, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "you made the new top record";
				mes "this time. Congratulations!";
				set $050320_minus1_typing$, strcharinfo(0);
				set $050320_ein_typing, .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "is the current";
				mes "record holder with";
				mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
				mes "characters. Try to beat";
				mes "that record next time~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "I'm sorry, but";
			mes "you entered the";
			mes "text incorrectly...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "The ^ff0000RO Typing Challenge^000000";
		mes "is a game where you enter";
		mes "the given text as quickly as you";
		mes "can. The name of the top player";
		mes "is recorded for posterity. If you";
		mes "want fame, here's your chance!";
		next;
		mes "[Nils]";
		mes "I'd just like to let";
		mes "you know that you type";
		mes "all the text that you see";
		mes "in the single input line that";
		mes "you're given. So don't press";
		mes "the enter key, just click 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "is the current";
		mes "record holder with";
		mes "a letter total of ^0000ff" + $050320_ein_typing + "^000000";
		mes "characters. Try to beat";
		mes "that record next time~";
		close;
	case 4:
		mes "[Nils]";
		mes "Feel free to take on the";
		mes "^ff0000RO Typing Challenge^000000";
		mes "anytime. I'll be here~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Hi, I'm Clarice~";
	mes "How would you like";
	mes "to wager some Apples";
	mes "in a friendly game of Dice?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{

	switch (select("Play Dice Game:Learn Dice Game Rules:Cancel")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "I'm up for a game of";
		mes "dice whenever you feel";
		mes "like it. Just talk to me if";
		mes "you ever get hit with the";
		mes "sudden urge to gamble, kay?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "The rules for the Dice game";
		mes "are pretty simple. First, you";
		mes "place a bet by wagering Apples.";
		mes "You can bet a maximum of 50";
		mes "Apples at a time. To keep things";
		mes "legal, I can only accept Apples.";
		next;
		mes "["+getarg(0)+"]";
		mes "But hey, if all that zeny";
		mes "is burning a hole in your";
		mes "pocket, head over to Fruitz";
		mes "and you can buy as many";
		mes "Apples as you want, playah~";
		next;
		mes "["+getarg(0)+"]";
		mes "Now, we begin with me";
		mes "rolling two 6-sided dice.";
		mes "When it's your turn, you'll";
		mes "roll two 6-sided dice. After";
		mes "that, both of us will have the";
		mes "option of rolling a third die.";
		next;
		mes "["+getarg(0)+"]";
		mes "Now here's the important";
		mes "thing. If your total is higher";
		mes "than 12, you'll bust, meaning";
		mes "that you lose. Otherwise, the";
		mes "person with the higher total";
		mes "is the winner. Got it?";
		next;
		mes "["+getarg(0)+"]";
		mes "Now, you'll be the first";
		mes "to decide whether or not";
		mes "you'll roll the third die. Then,";
		mes "depending on your result, I'll";
		mes "roll my third die... Or maybe not.";
		next;
		mes "["+getarg(0)+"]";
		mes "When you win, you'll";
		mes "receive twice as many";
		mes "Apples as you wagered.";
		mes "But if we happen to tie, you";
		mes "get the Apples that you bet";
		mes "returned to you. Fair, right?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ooh, so you'll play with";
	mes "me? Great! How many";
	mes "Apples would you like to bet?";
	mes "Remember, you can wager";
	mes "up to 50 Apples. If you'd like";
	mes "to cancel, please enter '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Changed your mind?";
			mes "I understand. Well then,";
			mes "I hope we can play sometime.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "You can't bet more than";
			mes "50 Apples. Remember, we";
			mes "need to keep these stakes";
			mes "reasonable. Please enter";
			mes "a value no greater than 50.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "So you'll be";
		mes "betting ^FF0000"+.@amount+"^000000 Apples.";
		mes "Is that right?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+getarg(0)+"]";
			mes "Mm, made a mistake?";
			mes "Alright, please enter the";
			mes "number of Apples you";
			mes "wish to place in this bet";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "I'm sorry, but you";
			mes "don't seem to have";
			mes "enough Apples for this";
			mes "bet... You can't gamble";
			mes "if you can't play, you know.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Good!";
		mes "Now we can start";
		mes "this game! I'll roll first~";
		break;
	}
	mes "^3355FF*Rolling and rumbling*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+getarg(0)+"]";
	mes "I got a ^0000FF" + .@table1 + "^000000 and a ^0000FF" + .@table2 + "^000000.";
	mes "That's a total of ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, now it's your turn.";
	next;
	select("Cast Dice.");
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+getarg(0)+"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, you have ^FF0000" + .@player1 + "^000000 and ^FF0000" + .@player2 + "^000000. The total is ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+getarg(0)+"]";
	if(.@playersub == .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. We are making an even game. Would you like to cast dice again?";
	} else if (.@playersub > .@tablesub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, you are currently winning this game. Would you like to cast dice again?";
	} else if(.@tablesub > .@playersub) {
		mes "Currently my total is ^0000FF" + .@tablesub + "^000000 and ^FF0000" + strcharinfo(0) + "^000000, your total is ^FF0000" + .@playersub + "^000000. I am winning this game. Would you like to cast dice again?";
	}
	next;
	switch (select("Cast dice.:Cancel.")) {
	case 1:
		mes "^3355FF*Rolling and rumbling*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+getarg(0)+"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. You lost this game. I am sorry but please try again.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Even though you casted dice again, still your total is smaller than mine. You lost the game. I am sorry and please try again.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. I don't want to take any risk, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, you got ^FF0000" + .@player3 + "^000000 and the total is now ^FF0000" + .@playertotal + "^000000. Now it is my turn.";
		}
		break;
	case 2:
		mes "["+getarg(0)+"]";
		if (.@playersub > .@tablesub) {
			mes "I see, you don't want to take risk of losing the game. Okay, let me cast dice again.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "I see, you don't want to take risk of losing this game. Neither do I, let's end this game in a draw. Let's play again some other time~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Alright.";
			mes "Let me cast the dice again.";
		}
		else {
			mes "It couldn't hurt to try.";
			mes "Well, I win this time.";
			mes "I'm sorry, let's try play";
			mes "again sometime.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolling and rumbling*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+getarg(0)+"]";
	if (.@tabletotal > 12) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. I lost this game since my total exceeded 12. Let me give you my apples. Congratulations, that was a great game.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you won this game, ^FF0000" + strcharinfo(0) + "^000000. Let me give you my apples. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 this game came out even, ^FF0000" + strcharinfo(0) + "^000000. Let me give you your apple back. It was a great game and I hope we will play again some other time.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "I got ^0000FF" + .@table3 + "^000000 and the total is now ^0000FF" + .@tabletotal + "^000000. With total ^FF0000" + .@playertotal + "^000000 you lost this game, ^FF0000" + strcharinfo(0) + "^000000. I am sorry but please try again.";
		close;
	}
}
